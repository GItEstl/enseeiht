grammar fr.enseeiht.gls.Game with org.eclipse.xtext.common.Terminals

generate game "http://www.enseeiht.fr/gls/Game"

Jeu:
	'jeu' name=ID
	'dans' territoire=[Territoire]
	'avec' explorateur=[Explorateur]
	// default: difficulte=Moyen
	(=> 'difficulte' difficulte=Difficulte)?
	composants+=Composant*
;

Composant:
	Territoire
	| ComposantTerritoire
	| Personne
	| Explorateur
	| Connaissance
	| ObjetElementaire
	| Condition
	| Choix
	| Action
;

Territoire:
	'territoire' name=ID
	'couvre' composants+=[ComposantTerritoire]+
;

ComposantTerritoire:
	Lieu
	| Chemin
;

Lieu:
	'lieu' name=ID
	// default: possession=null
	(=> 'possede' (=> possessions+=PossessionConditionnee ',')* possessions+=PossessionConditionnee)?
	// default: personnes=null
	(=> 'accueille' personnes+=[Personne]+)?
	// default: position=intermediaire
	(=> 'position' position=Emplacement)?
	// default: conditions=null
	(=> 'selon' conditions+=[Condition]+)?
	// default: descriptions=null
	(=> 'description' descriptions+=DescriptionConditionnee+)?
;

DescriptionConditionnee:
	// default: conditions=null
	'{'description=STRING'}' (=> 'selon' conditions+=[Condition]+)?
;

Chemin:
	'chemin' name=ID
	'depuis' depart=[Lieu]
	'vers' fin=[Lieu]
	// default: ouverture=ouvert conditionsOuverture=null
	(=> 'ouverture' ouverture=Ouverture (=> 'selon' conditionsOuverture+=[Condition]+)?)?
	// default: obligation=facultatif
	(=> 'passage' obligation=Obligation)?
	// default: visibilite=visible conditionsVisibilite=null
	(=> visibilite=Visibilite (=> 'selon' conditionsVisibilite+=[Condition]+)?)?
	// default: possessions=null
	(=> 'possede' (=> possessions+=PossessionConditionnee ',')* possessions+=PossessionConditionnee)?
;

Personne:
	'personne' name=ID
	// default: passage=facultatif
	(=> 'passage' obligation=Obligation)?
	// default: possessions=null
	(=> 'possede' (=> possessions+=PossessionConditionnee ',')* possessions+=PossessionConditionnee)?
	// default: choix=null
	(=> 'offre' choix+=[Choix]*)?
;

Explorateur:
	'explorateur' name=ID
	// default: possessions=null
	(=> 'possede' (=> possessions+=PossessionConditionnee ',')* possessions+=PossessionConditionnee)?
	'porte' poidsMax=INT
;

Possession:
	connaissance=[Connaissance]
	| Objet
;

PossessionConditionnee:
	// default: conditions=null
	possession=Possession (=> 'selon' conditions+=[Condition]+)?
;

Connaissance:
	'connaissance' name=ID
	// default: visibilite=visible conditionsVisibilite=null
	(visibilite=Visibilite (=> 'selon' conditionsVisibilite+=[Condition]+))?
;

Objet:
	// default: quantite=1
	(=> quantite=INT)? objet=[ObjetElementaire]
;

ObjetElementaire:
	'objet' name=ID
	'mesure' taille=INT
	// default: visibilite=visible conditionsVisibilite=null
	(=> visibilite=Visibilite (=> 'selon' (conditionsVisibilite+=[Condition])+)?)?
	// default: transformabilite=immuable conditionsTransformabilite=null
	(=> transformabilite=Transformabilite conditionsTransformabilite+=Transformation*)?
;

Transformation:
	 'en' objets+=Objet+ 'selon' conditions+=[Condition]+
;

Condition:
	Decision|
	'condition' name=ID
	// default: possessions=null
	(=> 'possede' possessions+=Comparaison+)?
	// default: conditions=null operateur=et
	(=> 'selon' conditions+=[Condition]+ (=> 'operateur' operateur=Operateur)?)?
	// default: difficulte=null
	(=> 'difficulte' difficulte+=Difficulte+)?
	// On note seulement la probabilite est entre 0 et 100
	// default: probabilte=100
	(=> '(probabilite' probabilite=INT ')')?
;

Comparaison:
	connaissance=[Connaissance]
	| comparateur=Comparateur objet=Objet
;

Choix:
	'choix' name=ID
	// default: actions=null
	(=> 'propose' (actions+=ActionConditionnee ',')* actions+=ActionConditionnee)?
	// Un meme choix ne peut pas etre de depart et de fin
	// default: position=null conditions=null
	(=> 'position' position=Emplacement (=> 'selon' conditions+=[Condition]+)?)?
;

ChoixConditionne:
	choix=[Choix]
	// default: conditions=null
	(=> 'selon' conditions+=[Condition]*)?
	// default: actionsPrecedentes=null
	(=> 'selon' (=> decisionsPrecedentes+=Decision ',')* decisionsPrecedentes+=Decision)?
;

Action:
	'action' name=ID
	// default: possessions=null
	(=> 'possede' (=> possessions+=PossessionConditionnee ',')* possessions+=PossessionConditionnee)?
	// default: objetsConsommes=null
	(=> 'consomme' objetsConsommes+=Objet+)?
;

ActionConditionnee:
	action=[Action]
	// default: conditions=null
	(=> 'selon' conditions+=[Condition]*)?
	// default: actionsPrecedentes=null
	(=> 'selon' (=> decisionsPrecedentes+=Decision ',')* decisionsPrecedentes+=Decision)?
	('offre' choixOfferts+=ChoixConditionne)+
;

Decision:
	choix=[Choix] 'propose' action=[Action]
;

enum Difficulte:
	Moyen='moyen'
	| Facile='facile'
	| Difficile='difficile'
	;
	
enum Emplacement:
	Intermediaire='intermediaire'
	| Depart='depart'
	| Fin='fin'
;

enum Visibilite:
	Visible='visible'
	| Invisible='invisible'
;

enum Ouverture:
	Ouvert='ouvert'
	| Ferme='ferme'
;

enum Obligation:
	Facultatif='facultatif'
	| Obligatoire='obligatoire'
;

enum Transformabilite:
	Immuable='immuable'
	| Transformable='transformable'
;

enum Comparateur:
	Egal='exactement'
	| Superieur='plus'
	| Inferieur='moins'
;

enum Operateur:
	EtLogique='et'
	| OuLogique='ou'
	| NonLogique='non'
;